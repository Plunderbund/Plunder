apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def gitSha = null

try {
  gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
} catch (Exception e) {
  logger.warn("git not available", e)
}

def versionMajor = 0
def versionMinor = 1
def versionPatch = 5

repositories {
  mavenCentral()
  maven { url "https://clojars.org/repo/" }
  maven { url "https://jitpack.io" }
}

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.3.1'
  }
}

retrolambda {
  javaVersion JavaVersion.VERSION_1_7
}

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.0"

  defaultConfig {
    applicationId "com.plunder.plunder"
    minSdkVersion 21
    targetSdkVersion 25
    multiDexEnabled true

    versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "TMDB_API_KEY", "\"a4b2e4479449209934eb7a2653054c44\""
    buildConfigField "String", "GITHUB_NAME", "\"Plunderbund/Plunder\""

    buildConfigField 'String', 'BUILD_NUMBER', (getBuildNumber() == null ? 'null' : "\"${buildNumber}\"")
    buildConfigField 'String', 'GIT_SHA', (gitSha == null ? 'null' : "\"${gitSha}\"")
  }
  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
      resValue "string", "app_name", "Plunder (debug)"
      minifyEnabled false
    }
    release {
      resValue "string", "app_name", "Plunder"
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  lintOptions {
    abortOnError false
  }
  packagingOptions {
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
  }
}

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')

  // support libraries
  compile 'com.android.support:support-v4:25.0.1'
  compile 'com.android.support:appcompat-v7:25.0.1'
  compile 'com.android.support:leanback-v17:25.0.1'
  compile 'com.android.support:support-annotations:25.0.1'
  compile 'com.android.support:recommendation:25.0.1'
  compile 'com.android.support:design:25.0.1'
  compile 'com.android.support:preference-leanback-v17:25.0.1'
  compile 'com.android.support:recyclerview-v7:25.0.1'
  compile 'com.android.support:cardview-v7:25.0.1'
  compile 'com.android.support:multidex:1.0.1'

  // debugging
  debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
  releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
  testCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
  compile 'com.jakewharton.timber:timber:4.3.1'
  debugCompile 'com.nshmura:strictmode-notifier:0.9.3'
  releaseCompile 'com.nshmura:strictmode-notifier-no-op:0.9.3'

  // core
  apt 'com.google.dagger:dagger-compiler:2.7'
  compile 'com.google.dagger:dagger:2.7'
  provided 'org.glassfish:javax.annotation:10.0-b28'
  compile 'com.annimon:stream:1.1.3'
  compile 'org.greenrobot:eventbus:3.0.0'
  provided 'com.google.auto.value:auto-value:1.2'
  apt 'frankiesardo:auto-parcel:1.0.3'
  compile 'com.google.guava:guava:20.0'
  compile 'commons-io:commons-io:2.5'
  compile 'com.jakewharton:butterknife:8.4.0'
  apt 'com.jakewharton:butterknife-compiler:8.4.0'
  compile 'com.mikhaellopez:circularprogressbar:1.1.1'

  // rxjava
  compile 'io.reactivex:rxandroid:1.2.1'
  compile 'io.reactivex:rxjava:1.2.2'
  retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:+'

  // networking
  compile 'com.github.bumptech.glide:glide:3.7.0'
  compile 'com.squareup.okhttp3:okhttp:3.4.2'

  // media
  compile 'com.github.Plunderbund:TorrentStream-Android:e923aab805'
  compile 'com.uwetrottmann.tmdb2:tmdb-java:1.3.0'
  compile 'com.github.sreichholf:vlc-android-sdk:b6a41d4614'

  compile 'com.trello:rxlifecycle:1.0'
  compile 'com.trello:rxlifecycle-android:1.0'

  compile 'com.github.Plunderbund:Provider:c94a35e60e'
  compile group: 'org.kohsuke', name: 'github-api', version: '1.79'
  compile group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0'
  compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.0@aar'
  compile 'org.nanohttpd:nanohttpd:2.3.1'
}

apply from: "signing.gradle"

String getBuildNumber() {
  String travisBuildNumber = System.getenv("TRAVIS_BUILD_NUMBER")

  if(travisBuildNumber != null) {
    return "T-$travisBuildNumber"
  }

  return null;
}